% --- model of a single fuel pebble from the Xe-100 design

% --- for triso particle, need fuel core, buffer, pyro-carbon. and silicon carbide mats

set acelib sss_jeff312.xsdata

/************************
 * Material definitions *
 ************************/

% --- fuel: UCO , guessing enrichment of 15% for now. using
% --- density/chemical formula of AGR2 UCO from nagley et. all "fabrication..."

mat fuel      -11.0 burn 1
92235.09c     -0.132848
92238.09c     -0.762437
 8016.09c     -0.086060
 6000.09c     -0.017760

% --- coolant: Helium, assuming all he-4

mat cool      -0.00408
 2004.09c     1.00

% --- reflector: HTGR graphite - have notes on this graphite, going with
% --- nat comp for now

mat graph    -1.74
 6000.09c    9.99E-01
 5010.09c    4.975E-07
 5011.09c    2.0025E-06

% --- graph mat can be used for carbon buffer in TRISO particle
% --- below input other TRISO mats

mat pyroc   -2.2
 6000.09c   1.0

mat sic     -3.1
14028.09c   0.4611
14029.09c   0.02343
14030.09c   0.01546
 6000.09c   0.5% unsure of bc choice

therm MyThermLib lw10.32t

 /************************
  * Geometry definitions *
  ************************/
% current issue: particle distribution file created assuming
% particles are in the entire 3cm radii, but in actuality, the outer .5cm
% of the sphere has no triso particles in it

particle 1

fuel        0.02125
graph       0.03075
pyroc       0.03475
sic         0.03825
pyroc       0.04225
void

% --- based on HGTR tutorial from serpent wiki
% confused; described in example as surface surrounding particles
% but what surrounds the particles is a sphere of graphite
% not an infinite surface??  is it because nested surfaces are defined
% as being surrounded in an infinite material??

surf 10 inf

cell 10 2 graph -10

pbed 10 2 "prtcles.inp"

% --- describing pebble: graphite

surf 1 sph  0.0 0.0 0.0 2.5
surf 2 sph  0.0 0.0 0.0 3.0
surf 3 cube 0.0 0.0 0.0 3.0

cell 50 0 fill 10  -1
cell 60 0 graph     1 -2
cell 70 0 cool      2 -3
cell 80 0 outside   3


% run parameters

set pop 7000 500 50

set bc 2

% designating as 2 group, cutoff at thermal energy:

set nfg 0.625E-6

plot 3  200  200

plot 3 1000 1000 0.0 -3.5 3.5 -3.5 3.5

plot 2  200  200

mesh 3  200  200

% decay/fission yield libraries

set declib sss_jeff33.dec
set nfylib sss_jeff33.nfy
