/*This input file defines a hexagonal elementary cell for the FCM fuel.*/

/**********************************
*       Particle Definition       *
* Kernel UN Diameter (D) 700e-6 m *
* Buffer Thickness (T) 50e-6 m    *
* IPyC T 35e-6 m                  *
* SiC T 35e-6 m                   *
* OPyC T 20e-6 m                  *
* Matrix SiC                      *
**********************************/
% -- Coated Particle
particle 1

un     0.035
buffer 0.04
pyc    0.0435
sic    0.047
pyc    0.049
sic

surf 1 inf
cell 1 2 sic -1
pbed 10 2 "dispersed_particles.inp"

% -- Fuel Channel
surf 10 cyl 0.0 0.0 0.7 0.0 1.0
surf 11 cyl 0.0 0.0 1.0 0.0 1.0
surf 12 hexyprism 0.0 0.0 2.3 0.0 1.0

cell 2 0 fill 10 -10
cell 3 0 helium   10 -11
cell 4 0 block    11 -12
cell 5 0 outside  12

/**********************************
*            Materials            *
* Kernel UN Density (d) 14.32g/cc *
* - U (e% = 12%)                  *
* - N 8.11%                       *
* Buffer d 1.05g/cc               *
* - C 100%                        *
* PyC d 1.9g/cc                   *
* - C 100%                        *
* SiC d 3.18g/cc                  *
* - Si 70.05%                     *
* - C  29.96%                     *
**********************************/
mat un       -14.32
 92235.09c   -0.110268
 92238.09c   -0.808632
  7014.09c   -0.0811

mat buffer   -1.05        moder grph 6000
 6000.09c    -1.0000

mat pyc      -1.90        moder grph 6000
 6000.09c    -1.0000

mat sic      -3.18        moder grph 6000
14000.09c    -0.7005
 6000.09c    -0.2996

mat block    -1.75        moder grph 6000
 6000.09c    -1.0000

mat helium   2.65156E-05
 2004.09c    2.65156E-05

mat reflector   -3.01
 4009.09c    0.50
 8016.09c    0.50

% --- Thermal scattering data for light water:

therm grph gre7.00t

% --- Cross section data library file path:

set acelib "/home/roberto/Serpent2/xs/sss_jeff311u"

% --- Periodic boundary condition:

%set bc 3

% --- Neutron population and criticality cycles:

set pop 5000 100 40

% --- Geometry plot:

plot 3 600 600 0.2 -2.7 2.7 -2.4 2.4
plot 1 1000 500 0.0 -2.3 2.3 -0.0 1.0